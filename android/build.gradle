apply plugin: 'com.android.library'

def DEFAULT_COMPILE_SDK_VERSION             = 26
def DEFAULT_BUILD_TOOLS_VERSION             = "28.0.2"
def DEFAULT_TARGET_SDK_VERSION              = 26
def DEFAULT_ANDROID_SUPPORT_VERSION         = "26.1.0"
def DEFAULT_EXOPLAYER_VERSION               = "2.9.6"

import groovy.json.JsonSlurper

def computeVersionName() {
    // dynamically retrieve version from package.json
    def slurper = new JsonSlurper()
    def json = slurper.parse(file('../package.json'), "utf-8")
    return json.version
}

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

android {
    compileSdkVersion rootProject.hasProperty('compileSdkVersion') ? rootProject.compileSdkVersion : DEFAULT_COMPILE_SDK_VERSION
    buildToolsVersion rootProject.hasProperty('buildToolsVersion') ? rootProject.buildToolsVersion : DEFAULT_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion rootProject.hasProperty('targetSdkVersion') ? rootProject.targetSdkVersion : DEFAULT_TARGET_SDK_VERSION
        versionCode 1
        // get version name from package.json version
        versionName computeVersionName()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    google()
}

dependencies {
    def androidSupportVersion = rootProject.hasProperty("androidSupportVersion")  ? rootProject.androidSupportVersion : DEFAULT_ANDROID_SUPPORT_VERSION
    def exoPlayerVersion = rootProject.hasProperty("exoPlayerVersion")  ? rootProject.androidSupportVersion : DEFAULT_EXOPLAYER_VERSION

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.google.android.exoplayer:exoplayer:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:extension-rtmp:$exoPlayerVersion"
    implementation 'com.facebook.react:react-native:+'
}
